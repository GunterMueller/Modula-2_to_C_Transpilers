(* ------------------------------------------------------------------ *
 *                 BMB Modula-2 Cross-Platform Library                *
 *                                                                    *
 * Implementation of:       Console                                   *
 *                          (see Console.doc for full specification!) *
 * Version:                 1.10                                      *
 * Date:                    October, 1996                             *
 * Conformance Test Passed: -                                         *
 * Platform:                DOS/80x86                                 *
 * Requires:                TopSpeed Modula-2 Compiler Version 1.xx   *
 *                                                                    *
 * Author:                  Bojan Skaljak                             *
 *                            University of Novi Sad, Yugoslavia      *
 * ------------------------------------------------------------------ *)

DEFINITION MODULE Console;

IMPORT Key;

   (* Error codes. Some of these will be set by some of
      the following operations. Once set, error codes will
      remain in effect until:
      - new error code is generated
      - function 'Error' is called (that will reset the code to 'noError'.
      Only the call to 'Error' will reset the error code back to 'noError'!

      If some operation sets more than one error code, then the error
      code with highest priority will be set.

      Error codes are the following: (in order of reversed priority)
   *)

CONST
    noError             =  0;    (* No Error *)
    unknownError        =  1;    (* Any other error then described below *)
    noRoomError         =  2;    (* Buffer space exceeded *)
    wrongCoordError     =  3;    (* At least one coordinate is wrong *)

    cannotScroll        =  4;    (* Scroll option is off *)
    cannotSetStyle      =  5;    (* Undefined or unavailable style *)
    cannotSetColor      =  6;    (* Unavailable color *)
    cursorOutOfRegion   =  8;    (* Cursor is out of defined region *)
    notTextMode         =  9;    (* !!! ? *)

    ColorDefault	= 16;

(* Style definitions *)

    StylePlain		=   0;
    StyleInverse	=   8;
    StyleBlink		= 128;
    StyleInverseBlink	= 130;

TYPE
   ColorType		= CARDINAL;
   StyleType		= CARDINAL;

CONST
    On  = TRUE;
    Off = FALSE;


PROCEDURE RdChar(): CHAR;
   (* Returns character read from keyboard *)

PROCEDURE RdLine(VAR str: ARRAY OF CHAR);
   (* Reads line from keyboard
      + Sets error code
        'noRoomError' - input string length exceeded len of 'str' *)

PROCEDURE RdLn();
   (* Skip all character till (and including) the end of line. *)

(* ----------- *)

PROCEDURE SetEchoOn;
   (* Turns echo on (influence only to RdChar & RdLine) *)

PROCEDURE SetEchoOff;
   (* Turns echo off (influence only to RdChar & RdLine) *)

PROCEDURE EchoOn(): BOOLEAN;
   (* Returns echo status *)

PROCEDURE ClearScreen;
   (* Clears entire screen *)

PROCEDURE ClearScrollRegion;
   (* Clears scroll region *)

PROCEDURE SetScrollRegion(x1,y1,x2,y2: CARDINAL);
   (* Sets scroll region (window). Wrapping & scrolling affects only that
      region. If output is done outside that region wrap & scroll
      are set to off.
      + Sets error code:
        'wrongCoordError' - if coordinates are invalid
   *)

PROCEDURE GetScrollRegion(VAR x1,y1,x2,y2: CARDINAL);
          (* Returns scroll region(window) coordinates.Default region
             is set to 1,1,maxX,maxY. *)

PROCEDURE ScrollUp;
          (* Scrolls inside of the region up.Last line will be cleared
             + Sets error code
               'cannotScroll'  - if scroll mode is off *)

PROCEDURE ScrollDown;
          (* Scrolls inside of the region down.First line will be cleared
             + Sets error code
               'cannotScroll'  - if scroll mode is off *)

PROCEDURE SetScrollOn;
   (* Turns scroll on *)

PROCEDURE SetScrollOff;
   (* Turns scroll off *)

PROCEDURE ScrollOn(): BOOLEAN;
   (* Returns TRUE if scroll is on *)

PROCEDURE SetWrapOn;
   (* Turns wrap on *)

PROCEDURE SetWrapOff;
   (* Turns wrap off *)

PROCEDURE WrapOn(): BOOLEAN;
   (* Returns TRUE if wrap is on *)

PROCEDURE SetStyle(style:StyleType);
          (* Set style used for output procedures
             + Sets error code:
               'cannotSetStyle' - wrong or unavailable style
          *)

PROCEDURE GetStyle():StyleType;
          (* Returns style currently in use*)

PROCEDURE SetColorTo(color:ColorType);
          (* Set color used for output procedures
             + Sets error code:
               'cannotSetColor' - unavailable color
          *)

PROCEDURE GetColor():ColorType;
          (* Returns color currently in use *)

PROCEDURE SetBackGround(color:ColorType);
          (* Set color of background
             + Sets error code:
               'cannotSetColor' - unavailable color
          *)

PROCEDURE GetBackGround():ColorType;
          (* Returns background color *)

PROCEDURE WrChar(c:CHAR);
          (* Writes character to screen*)

PROCEDURE WrStr(str:ARRAY OF CHAR);
          (* Writes string to screen *)

PROCEDURE WrLn;
          (* Goes to new line *)

PROCEDURE Redraw;
          (* Restoration of previous screen contents *)

PROCEDURE SetRedrawOn;
   (* Turns redraw on, and after that every output procedure
      will save screen before output is done *)

PROCEDURE SetRedrawOff;
   (* Turns redraw off *)

PROCEDURE RedrawOn(): BOOLEAN;
   (* Returns status of redraw *)

(* Next procedures does whatever in scroll region from place of cursor *)

PROCEDURE ClearToEOD;
          (* Erase to the end of display(region)
             + Sets error code:
               'cursorOutOfRegion' - Cursor is out of valid scroll region
          *)

PROCEDURE ClearToEOL;
          (* Erase to the end of line
             + Sets error code:
               'cursorOutOfRegion' - Cursor is out of valid scroll region
          *)

PROCEDURE InsertLine;
          (* Insert line at cursor position
             + Sets error code:
               'cursorOutOfRegion' - Cursor is out of valid scroll region
          *)

PROCEDURE DeleteLine;
          (* Delete line at cursor position
             + Sets error code:
               'cursorOutOfRegion' - Cursor is out of valid scroll region
          *)

PROCEDURE InsertChars(n:CARDINAL);
          (* Insert n chars, pushing line to right if wrap is on
             + Sets error code:
               'cursorOutOfRegion' - Cursor is out of valid scroll region
          *)

PROCEDURE DeleteChars(n:CARDINAL);
          (* Delete n chars, moving rest of line to the left
             + Sets error code:
               'cursorOutOfRegion' - Cursor is out of valid scroll region
          *)



   (* Errors *)

   PROCEDURE Error(): CARDINAL;
      (* Returns an error code and resets it to 'noError'.
         See the possible error codes in constant declaration part *)
TYPE
   ErrorHandler = PROCEDURE(ARRAY OF CHAR);
VAR
   errorHandler: ErrorHandler;
      (* Initially contains procedure that will in case of an error
         print the appropriate message and stop the program execution. It
         can be replaced with user-defined procedure of type ErrorHandler.
      *)

END Console.

(*
   Changes from ver. 1.00:
   - constants for error codes are slighly changed
   - semantics of all operation clarified
   - error handler 'errorHandler' added
*)
